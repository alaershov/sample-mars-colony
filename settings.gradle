pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
    versionCatalogs {
        libs {
            library('androidx-core', 'androidx.core:core-ktx:1.17.0')
            library('androidx-lifecycle-runtime', 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4')
            library('androidx-activity-compose', 'androidx.activity:activity-compose:1.11.0')

            version('androidx-compose-bom', '2025.09.00')
            library('androidx-compose-bom', 'androidx.compose', 'compose-bom').versionRef('androidx-compose-bom')
            library('androidx-compose-ui-ui', 'androidx.compose.ui', 'ui').withoutVersion()
            library('androidx-compose-ui-ui-tooling', 'androidx.compose.ui', 'ui-tooling').withoutVersion()
            library('androidx-compose-material3', 'androidx.compose.material3', 'material3').withoutVersion()
            library('androidx-compose-material', 'androidx.compose.material', 'material').withoutVersion()
            bundle(
                    'compose',
                    [
                            'androidx-compose-ui-ui',
                            'androidx-compose-ui-ui-tooling',
                            'androidx-compose-material3',
                            'androidx-compose-material',
                    ]
            )

            version('decompose', '3.3.0')
            library('decompose', 'com.arkivanov.decompose', 'decompose').versionRef('decompose')
            library('decompose-extensions-compose', 'com.arkivanov.decompose', 'extensions-compose').versionRef('decompose')
            bundle(
                    'decompose',
                    [
                            'decompose',
                            'decompose-extensions-compose'
                    ]
            )

            version('dagger', '2.57.1')
            library('dagger-core', 'com.google.dagger', 'dagger').versionRef('dagger')
            library('dagger-compiler', 'com.google.dagger', 'dagger-compiler').versionRef('dagger')
        }
    }
}
rootProject.name = "Mars Colony"
include ':app'
include ':ui'
include ':bottom-sheet'
include ':feature-root'
include ':feature-dashboard'
include ':feature-habitat'
include ':feature-power'
include ':shared-weather'
include ':feature-message-dialog'
